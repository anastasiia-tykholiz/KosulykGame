
=== CollectForestPlants ===
{ CollectForestPlantsQuestState :
    - "REQUIREMENTS_NOT_MET" : -> requirementsNotMet
    - "CAN_START" : -> canStart
    - "IN_PROGRESS" : -> inProgress
    - "CAN_FINISH" : -> canFinish
    - "FINISHED" : -> finished
    - else: -> END
}

= requirementsNotMet
-> END

= canStart
Косулик: Бабусю… чому ти така бліда?

* [Що трапилося?]          -> step_what

= step_what
    Бабуся: Ох, мій живіт… бурчить і стискається.
    Бабуся: Я саме з’їла яблука з нашого садка… дуже соковиті, але ти ж їх не помив…
    Косулик: . . .

    * [Вибач..]            -> step_apology


= step_apology
    Косулик: Вибач.. я забув... Але ти ж найкраще знаєш лікарські трави в цьому лісі, хіба ти не маєш ліки від цієї недуги?
    Бабуся: Ох, ти правий, але останні запаси я якраз віддала іншим мешканцям лісу.
    Косулик: Оо! О! Я можу піти зібрати! Я вже ж достатньо дорослий для цього, правда? 
    Бабуся: Так, Косулику, думаю твій час настав.
    Бабуся: За садком є ліс, де ростуть жовті квіти.
    * [Нагідки?]        -> step_flowers


= step_flowers
    Бабуся: Так, але будь обережний — не потрап в кропиву, її там теж багато.
    Косулик: Так я знаю… вона сильно пече, але якщо прикласти подорожник, то буде трохи легше!
    Бабуся: Молодець, думаю трьох нагідок вистачить для початку.
    * [Добре!]      -> step_ok


= step_ok
    Косулик: Я тоді одразу піду!!
    ~ StartQuest(CollectForestPlantsQuestId)
    -> END


= inProgress
Бабуся: Так, Косулику?
* [Нагадай..]
    Косулик: Нагадай що потрібно зібарти та де дубовий ліс?
    Бабуся: Три нагідки, жовті квіти, що ростуть в лісі ліворуч, трохи далі за наш садок.
    Бабуся: Там є табличка зі знаком лісу.
* [Що робити з квітами?]
    Бабуся: Як збереш всі квіти, підійти до казана та натисни "E".
- -> END

= canFinish
    Косулик: Я зробив все як було написано в книзі!
* [Віддати зілля]
    Бабуся: Ти зварив своє перше зілля! Дякую Косулику.
    ~ FinishQuest(CollectForestPlantsQuestId)
-> END

= finished
Косулик: Тобі ж стане легше?
Бабуся: Побачимо вранці… Дякую, мій відважний помічникую На добраніч.
* [Сподіваюсь..]
    Косулик: Сподіваюсь, зілля допоможе
    Косулик: На добраніч!
-> END

