
=== CollectPineForestPlants ===
{ CollectPineForestPlantsQuestState :
    - "REQUIREMENTS_NOT_MET" : -> requirementsNotMet
    - "CAN_START" : -> canStart
    - "IN_PROGRESS" : -> inProgress
    - "CAN_FINISH" : -> canFinish
    - "FINISHED" : -> finished
    - else: -> END
}

= requirementsNotMet
-> END

= canStart
Косулик: Бабусю, тобі вже трохи легше після нагідок, але...
* [Ти все ще бліда]         -> ask_need


= ask_need
    Бабуся: Болі вщухли, та гіркота ще стоїть у роті, а горло дере.  
    Бабуся: Думаю час перейти до сильніших квітів, до **звіробою**, що пахне смолою та збирається на схилах.  
    Бабуся: Однак до соснового лісу далеко йти, навіть якщо вирушиш на світанку, прийдеш туди вночі.

    * [Як туди дійти?]   -> advice_start


= advice_start
    Бабуся: Праворуч від нашої хатинки, прямо вгору, на соснові схили.
    Косулик: Але слихи ж такі високі! Як я дострибну?
    Бабуся: Створюючи зілля ти стаєш сильнішим, Косулику.
    Бабуся: Спробуй двічі швидко підстрибнути і слихи не будуть здаватися вже такими високими.
    Бабуся: Пам'ятай, поки світло тане, лисиці виходять. Та ти не лякайся, йди тихо далі, під лапи їм не попадайся. 
    * [Я.. спробую]          -> advice_end


= advice_end
    Бабуся: Принеси **п’ять суцвіть**: зрізай верхівку з 2-3 листочками та не мни — щоб не втратити лікувальний сік. 
    Косулик: Дякую за пораду! Я тоді вирушаю як найшвидше.
    * [У дорогу!]       -> start_quest


= start_quest
    Бабуся: Будь обережним, Косулику.
    ~ StartQuest(CollectPineForestPlantsQuestId)
    -> END


= inProgress
Бабуся: Так, Косулику?
* [Нагадай..]
    Косулик: Нагадай що потрібно зібрати та де сосновий ліс?
    Бабуся: Звіробою п'ять штук, жовті квіти, що ростуть в сосновому лісі праворуч у горах.
* [Що робити з квітами?]
    Косулик: Квіти знову потрібно в казан?
    Бабуся: Так, там буде відома тобі вже книга з рецептом.
- -> END


= canFinish
Косулик: Я зробив все, як ти казала!
* [Віддати зілля] 
    Бабуся: Чудово, запах смоли вже наповнює хату. Дякую, Косулику.
    Косулик: Коли тобі покращає?
    Бабуся: Не знаю, але я думаю скоро. Дякую за допомогу, на добраніч.
    Косулик: Сподіваюсь, зілля допоможе.
    Косулик: На добраніч!
    ~ EndDay(CollectSwampPlantsQuestId)
    ~ FinishQuest(CollectPineForestPlantsQuestId)
    -> END


= finished
-> END

